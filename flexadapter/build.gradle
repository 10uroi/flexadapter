apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.dcendents.android-maven'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath rootProject.ext.kotlinGradle
        classpath rootProject.ext.dokka
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    compile rootProject.ext.supportV7
    compile rootProject.ext.supportRecyclerView
    compile rootProject.ext.supportAnnotations
    compile rootProject.ext.kotlinStdlib

    testCompile rootProject.ext.robolectric
    testCompile rootProject.ext.junit
    testCompile rootProject.ext.mockito
    testCompile (rootProject.ext.mockitoKotlin) { exclude module: 'kotlin-stdlib' }
    testCompile rootProject.ext.assertJ
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
